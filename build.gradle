buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.+'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'com.company-name.new-project'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url "file:../maven-repo" }
    jcenter()
    mavenCentral()
}

configurations {
    all {
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    compile "io.vertx:vertx-hazelcast:$vertxVersion"
    compile "io.vertx:vertx-rx-java:$vertxVersion"
    testCompile 'org.testng:testng:+'

    if (project.hasProperty('combined') && project.combined) {
        compile project(':vertx-shared')
        compile project(':json-wrapper')
        compile project(':java-util')
    } else {
        compile 'com.canopy.vertx-shared:vertx-shared:+'
        compile 'com.canopy.json-wrapper:json-wrapper:+'
        compile 'com.canopy.java-util:java-util:+'
    }
}

test {
    useTestNG()
}

jar {
    from sourceSets.main.allJava
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Starter'
        attributes 'Main-Verticle': 'com.canopy.service.WebsocketService'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:../maven-repo")
        }
    }
}
